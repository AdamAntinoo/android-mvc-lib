// - G R A D L E - M E T R I C S
apply plugin: 'checkstyle'
apply plugin: 'pmd'

checkstyle {
    toolVersion = '8.1' // optionally specify tool version
}

pmd {
    toolVersion = '5.8.1' // optionally specify tool version
}

tasks.withType(Checkstyle) {
    ignoreFailures = true
    // REQUIRED because the plugin will report only up to the point the first issue if this is not set
    showViolations = false

    reports {
        xml.enabled = true  // REQUIRED the plugin parses xml reports so this is essential
        html.enabled = true
        html.stylesheet = resources.text.fromFile('config/xsl/checkstyle-custom.xsl')
    }

    include '**/*.java'
}

tasks.withType(Pmd) {
    ignoreFailures = true
    // REQUIRED because the plugin will report only up to the point the first issue if this is not set

    reports {
        xml.enabled = true  // REQUIRED the plugin parses xml reports so this is essential
    }

    include '**/*.java'
}

// - A N D R O I D
apply plugin: 'com.android.library'
apply plugin: 'project-report'
apply plugin: 'maven'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 31
        versionName "4.3.0"
        project.archivesBaseName = "AndroidMVCLibrary"
        project.version = android.defaultConfig.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "bool", "CACHE_MVC_VIEWS_ON_CONTROLLER", "true"
        }
        debug {
            initWith release
            resValue "bool", "CACHE_MVC_VIEWS_ON_CONTROLLER", "false"
        }
    }
    sourceSets {
        main {
            java.srcDirs += 'src/main/java-v3'
            res.srcDirs += 'src/main/res-ui'
            res.srcDirs += 'src/main/res-dynamic'
            res.srcDirs += 'src/main/res-v3'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
        quiet true
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

// - M A V E N   P U B L I S H   I N F O
group = 'org.dimensinfin'
version = project.version
uploadArchives {
    repositories {
        mavenLocal()
    }
}

// - B U I L D   T A S K S
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

//Upload android library to maven with javadoc and android sources
configurations {
    deployerJars
}

// - P R O D U C T   V E R S I O N S
ext {
    supportLibraryVersion = '28.0.0'
    jodaTimeVersion = '2.3'
    jacksonVersion = '2.9.4'
    fasterxmlJackson = '2.9.4'
    ormliteVersion = '4.48'
    retrofitVersion = '2.3.0'
}

// - A N D R O I D    S U P P O R T
dependencies {
    compileOnly 'androidx.fragment:fragment:1.0.0'
}

// - L I B R A R Y   D E P E N D E N C I E S
dependencies {
    compileOnly 'org.slf4j:slf4j-api:1.7.25'
    implementation "joda-time:joda-time:$jodaTimeVersion"
    implementation 'org.apache.commons:commons-lang3:3.7'

    implementation 'me.relex:circleindicator:2.1.0'
    implementation 'org.dimensinfin:CoreBase.core:3.0.1'
}

// - T E S T I N G
dependencies {
    testImplementation 'androidx.annotation:annotation:1.0.0'
    testImplementation 'androidx.fragment:fragment:1.0.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.slf4j:slf4j-simple:1.7.25'
    testImplementation 'joda-time:joda-time:2.9.9'
    testImplementation 'com.openpojo:openpojo:0.8.3'
    testImplementation 'org.mockito:mockito-core:2.22.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.11.0'
    testImplementation 'androidx.arch.core:core-testing:2.0.1'
    testImplementation "org.robolectric:robolectric:4.3-alpha-2"
}

// - A N D R O I D   T E S T I N G
dependencies {
    androidTestImplementation 'androidx.fragment:fragment:1.0.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'

    androidTestImplementation 'org.mockito:mockito-android:2.21.0'
    androidTestImplementation 'org.mockito:mockito-core:2.22.0'
//    androidTestImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-RC2'
    androidTestImplementation 'androidx.test:runner:1.2.0-alpha03'
    androidTestImplementation 'androidx.test:rules:1.2.0-alpha03'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
//    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-alpha03'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0-alpha03'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0-alpha03'
    androidTestImplementation('com.github.tmurakami:dexopener:1.0.2') {
        exclude group: 'com.android.support.test', module: 'runner'
    }
}
